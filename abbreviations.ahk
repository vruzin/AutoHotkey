
; ----------------------------------------------------------
; Сокращения
::ltd::Send2("dev")
::дев::Send2("dev")
::crom::Send2("cron")
:o:GA::Send2("Google Analitics")
:o:дднс::Send2("Добрый день. Вы на связи?")
:o:ДДД::Send2("Добрый день")
:o:ДУ::Send2("Доброе утро")
:o:ДВ::Send2("Добрый вечер")
:o:пож::Send2("Пожалуйста")
:o:кол::Send2("количество")
:*:@@::Send2("vruzin@ya.ru")
#!u::Send2("?utm_source=yandex&utm_medium=cpc&utm_campaign={campaign_id}&utm_content={ad_id}&utm_term={keyword}")
:*:кон1::Send2("Мои контакты: WhatsApp, Телефон: +79046464626; Skype: vruzin; Telegram: @vruzin (Лучше в Телеграмм)")
:*:кон2::Send2("Мои контакты: WhatsApp, Телефон - в профиле; Skype: vruzin; Telegram: @vruzin (Лучше в Телеграмм)")
::ДДП::Send2("Добрый день. Это Рузин Василий по поводу проекта: ")

; ----------------------------------------------------------
; В зависимости от времени суток пишет приветствие
::ДД::
Hello()
return
::LL::
Hello()
return

Hello(){
    if(A_Hour < 5)
      state = Доброй ночи
    else if(A_Hour < 10)
      state = Доброе утро
    else if(A_Hour < 17)
      state = Добрый день
    else
      state = Добрый вечер
    state=%state%.
    Send2(state)
}


#Persistent  ; Выполнять скрипт, пока не закроет пользователь.

; ----------------------------------------------------------

CapsLock & h:: ; горячая клавиша Win+H
; Получаем текущий выделенный текст. Вместо "ControlGet Selected" используется
; буфер обмена, так как он есть в большинстве редакторов
; (т.е. текстовых процессоров).  Сохраняем текущее содержимое буфера обмена,
; чтобы восстановить его позднее. Хотя обрабатывается только простой текст,
; это все же лучше, чем ничего:
SetNumLockState, Off
SetCapsLockState, Off

AutoTrim Off ; Сохраняет любой межстрочный интервал и пробел в конце текстовой строки в буфере обмена.
ClipboardOld = %ClipboardAll%
Clipboard = ; Чтобы обнаружение заработало, нужно начать с пустого значения.
Send ^c
ClipWait 1
if ErrorLevel ; Время ожидания ClipWait вышло.
  return
; Заменяем CRLF и/или LF на `n, чтобы использовать в строке автозамены опцию "send-raw" (R):
; Тоже самое делаем с любыми другими символами, иначе
; могут возникнуть проблемы в "сыром" режиме:
; Делаем эту замену вначале, чтобы избежать помех со стороны тех, которые идут далее.
StringReplace, Hotstring, Clipboard, ``, ````, All
StringReplace, Hotstring, Hotstring, `r`n, ``r, All ; В MS Word...`r работает лучше, чем `n.
StringReplace, Hotstring, Hotstring, `n, ``r, All
StringReplace, Hotstring, Hotstring, %A_Tab%, ``t, All
StringReplace, Hotstring, Hotstring, `;, ```;, All
Clipboard = %ClipboardOld% ; Восстанавливаем предыдущее содержимое буфера обмена.
; Каретка поля ввода (InputBox) устанавливается в более удобную позицию:
SetTimer, MoveCaret, 10
; Показываем поле ввода (InputBox), обеспечивая строку автозамены по умолчанию:
Text1 := "Напечатайте вашу аббревиатуру в указанном месте. "
Text2 := "`n"
Text3 =
(
  Пример: :R:btw`::by the way

  ГДЕ:
  R — опции,
  btw — сокращение (аббревиатура);

  ОПЦИИ:
  * (звездочка): в конце (пробел, точка или перевод строки) не требуется.
  ? (знак вопроса): строка автозамены запустится, даже если находится внутри другого слова.
  B0 (за буквой B идет цифра 0): стирание (автоматический забой) напечатанной вами аббревиатуры не производится.
  C: чувствительность к регистру. Регистр аббревиатуры должен точно совпадать с регистром
  C1: не подчиняется регистру, используемому при наборе текста.
  Kn: задержка нажатия клавиши. 0 рекомендуется; -1 нет задержки
  o (буква): опускает конечный символ (нет пробела в конце)
  R: сырой режим
  Подробно: https://ahk-wiki.ru/hotstrings
)
SetNumLockState, Off
SetCapsLockState, Off
Gui, Font, s48, Arial
InputBox, Hotstring, Новая автозамена, %Text1%%Text2%%Text3%,,757,413,,,,, :R:`::Send2("%Hotstring%")
if ErrorLevel <> 0 ; Пользователь нажал Cancel.
  return
IfInString, Hotstring, :R`:::
{
  MsgBox Вы не напечатали аббревиатуру. Строка автозамены не добавлена.
  return
}
; Иначе, добавляем строку автозамены и перезагружаем скрипт.
; Помещаем `n в начало, в случае, если в конце файла нет пустой строки.
FileAppend, `n%Hotstring%, %A_Scriptdir%\abbreviations.ahk
; FileAppend, `n%Hotstring%, %A_ScriptFullPath%
Reload
; В случае успешного завершения перезагрузка закроет этот экземпляр скрипта в режиме ожидания,
; поэтому строка ниже никогда не будет исполнена.
Sleep 200
Text1 := "Только что добавленная строка неверно отформатирована. "
Text2 := "Открыть файл для форматирования? "
Text3 := "Обратите внимание, что неисправные строки автозамены находятся внизу скрипта."
MsgBox, 4,, %Text1%%Text2%%Text3%
IfMsgBox, Yes, Edit
return

MoveCaret:
IfWinNotActive, Новая автозамена
  return
; Иначе, передвигаем курсор в поле ввода туда, где пользователь напечатает аббревиатуру.
Send {Home}{Right 3}
SetTimer, MoveCaret, Off
return



tt(text){
    ToolTip, %text%
    SetTimer, ttRemove, -2000
    return
}
ttRemove:
ToolTip
return


